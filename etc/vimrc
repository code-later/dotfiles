" -=- Welcome to my Vim config -=-

" **************** First things first: Some general configuration

set nocompatible                                              " Turn off vi-compatibility, must be the first config to set
filetype off                                                  " required for Vundle to work (See Vundle for details)

let mapleader = ','                                           " <Leader> is ','

set encoding=utf-8
set ai                                                        " Automatically set the indent of a new line (local to buffer)
set completeopt=longest,menu                                  " Only show completion menu for more then one option and use the longest common text
set timeout timeoutlen=1000 ttimeoutlen=100                   " Fix slow O inserts from Gary Bernhardt
set shell=/bin/sh                                             " some commands seem to have problems with zsh (i.e. RVM)
set nowrap                                                    " There should be no wrapping (at least not for source code).
set sidescroll=1                                              " Scroll 1 column at a time horizontally
set sidescrolloff=2                                           " Keep some columns left/right for sidescrolling
set equalalways                                               " Multiple windows, when created, are equal in size
set splitbelow splitright                                     " Define where new splits should be placed
set cursorline                                                " Highlight the line with the cursor
set hlsearch                                                  " highlight search
set incsearch                                                 " Incremental search, search as you type
set ignorecase smartcase                                      " Ignore case when searching
set ruler                                                     " Show the current line and column number
set showmode                                                  " Display the mode you're in.
set scrolloff=3                                               " Show 3 lines of context around the cursor.
set ttyfast                                                   " We got us a fast terminal (a.k.a. local machine)
set backspace=indent,eol,start                                " Intuitive backspacing.
set nonumber                                                  " Hide line numbers per default
set list listchars=trail:·,tab:>-,nbsp:¬,extends:»,precedes:« " Show invisble chars and tell how to display them
set nobackup                                                  " Don't make a backup before overwriting a file.
set nowritebackup                                             " And again.
set noswapfile                                                " Don't use swapfiles
set hidden                                                    " Handle multiple buffers better.
set laststatus=2                                              " Show the status line all the time
set title                                                     " Set the terminal's title
set clipboard=unnamed                                         " Copy to system pasteboard
set winwidth=79                                               " Try to make the current window this wide
set mouse=n                                                   " Scrolling with the Magic Trackpad is just too nice ;-)
set wildmenu                                                  " Enhanced command line completion.
set wildmode=list:longest,list:full                           " And how to complete on the command line
set nofoldenable                                              " Turn on folding
set foldmethod=syntax                                         " Folding based on syntax
set foldlevel=1                                               " Close all folds starting with the first
set spell spelllang=en_us                                     " Activate spell checking and set American as default

" Some files we just don't want to see here
set wildignore+=*.o,*.obj,.git,*.rbc,*.class,.svn,tmp/*,*.so,*.swp,*.zip " general stuff
set wildignore+=*.png,*.jpg,*.gif,*.jpeg " images
set wildignore+=*/vendor/gems/*,*/vendor/cache/*,Gemfile.lock,*/log/*,.sass-cache,*/coverage/* " Ruby related files
set wildignore+=*.pyc,*.pyo " Python
set wildignore+=*/node_modules/****************" " NodeJS

" :w!! to save with sudo
cmap w!! w !sudo tee % > /dev/null

" Enable auto-save / auto-write
set autowrite
set autowriteall
au BufLeave,FocusLost * silent! :wa

" Setup color scheme
syntax enable                                    " Enable syntax highlighting
set t_Co=256                                     " 256 colors
set background=dark                              " Me like it dark
" let base16colorspace=256                         " Access colors present in 256 colorspace
colorscheme pinkiepie                            " And me like it pink
match ErrorMsg '\%>100v.\+'                      " Highlight Characters that overstep 100 characters (via @moonglum)
match ErrorMsg '^\(<\|=\|>\)\{7\}\([^=].\+\)\?$' " Highlight VCS conflict markers




" **************** Vundle config

" Setup Vundle itself
set rtp+=~/.vim/bundle/vundle/
call vundle#rc()

" let Vundle manage Vundle (won't work otherwise)
Bundle 'gmarik/vundle'




" **************** Add some Bundles, because why would we have added Vundle?

" Polyglot: A collection of language packs, loaded on demand
Bundle "sheerun/vim-polyglot"

" At match #N out of M matches
Bundle 'henrik/vim-indexed-search'

" Fuzzy finder
Bundle 'kien/ctrlp.vim'

" Use the_silver_searcher (ag) from within vim
Bundle 'rking/ag.vim'

" Replace status line with Airline
Bundle 'bling/vim-airline'

" Markdown plugins
Bundle 'tpope/vim-markdown'
Bundle 'jtratner/vim-flavored-markdown'
Bundle 'nelstrom/vim-markdown-folding'

" For blogging
Bundle 'tpope/vim-liquid'

" The Vim-Git-Integration™
Bundle 'tpope/vim-fugitive'

" RVM integration
Bundle 'tpope/vim-rvm'

" Graphical undo history
Bundle 'sjl/gundo.vim'

" Align utility
Bundle 'godlygeek/tabular'

" Zoom into splits
Bundle 'vim-scripts/ZoomWin'

" Smarter pasting
Bundle 'sickill/vim-pasta'

" Commenting plugin
Bundle "itomtom/tcomment_vim"

" Help with HTML tags
Bundle 'tpope/vim-ragtag'

" Surround anything with anything (i.e. words with parantheses)
Bundle 'tpope/vim-surround'

" Make commands like surround work with the '.' command
Bundle 'tpope/vim-repeat'

" Some aliases
Bundle 'tpope/vim-unimpaired'

" Close structures automatically
Bundle 'tpope/vim-endwise'

" Emojis. For Vim. Nuff said.
Bundle "junegunn/vim-emoji"

" Close parenthesis automatically
Bundle 'Raimondi/delimitMate'

" Better support for Rails
Bundle 'tpope/vim-rails'

" Syntax checking
Bundle 'scrooloose/syntastic'




" **************** Configure the Bundles

" Setup up CtrlP
let g:ctrlp_match_window_reversed = 0
let g:ctrlp_open_new_file = 'h' " open newly created windows in a horizontal split
let g:ctrlp_use_caching = 0
let g:ctrlp_working_path_mode = ''
let g:ctrlp_custom_ignore =  {
  \ 'link': 'fejo-engines',
  \ }

let g:ctrlp_user_command = {
  \ 'types': {
   \ 1: ['.git', 'cd %s && git ls-files --exclude-standard -co'],
   \ 2: ['.ctrlp_root_dir', 'find %s -type f'],
  \ },
  \ 'fallback': 'find %s -type f'
\ }
let g:ctrlp_cmd = "CtrlPLastMode"
let g:ctrlp_extensions = ['line']

" Setup syntastic
if emoji#available()
  " Terminal Vim for example
  " Cats
  let g:syntastic_error_symbol = emoji#for('scream_cat')
  let g:syntastic_warning_symbol = emoji#for('crying_cat_face')
else
  " GUI MacVim for example
  let g:syntastic_error_symbol = '✗'
  let g:syntastic_warning_symbol = '⚠'
end

let g:syntastic_check_on_open = 1 " Check syntax when file is opened

" We don't want active syntax checks for everything
let g:syntastic_mode_map = { 'mode': 'active',
                           \ 'passive_filetypes': ['scss', 'css', 'sass', 'javascript', 'html'] }

" Use Powerline font symbols
let g:airline_powerline_fonts = 1
let g:airline_detect_paste = 0

" toggles the Gundo graph
nnoremap <F5> :GundoToggle<CR>




" **************** And now for some custom mappings

" Center display line after each search
nnoremap n nzz
nnoremap N Nzz
nnoremap * *zz
nnoremap # #zz
nnoremap g* g*zz
nnoremap g# g#zz

" Vertical and horizontal split then hop to a new buffer
noremap <Leader>v :vsp<cr>
noremap <Leader>h :split<cr>

" Clear the search highlighting
nnoremap <leader><cr> :nohlsearch<CR>
" Easier switching between previous and current buffer
nnoremap <leader><leader> <c-^>

" Toggle folding with <Space>
nnoremap <Space> za
vnoremap <Space> zf

" Toggle commenting
nnoremap <silent> <Leader>/ :TComment<cr>
inoremap <silent> <Leader>/ <Esc>:TComment<cr>i
vnoremap <silent> <Leader>/ :TComment<cr>

" Make cursor move by visual lines instead of file lines (when wrapping)
map <up> gk
map k gk
imap <up> <C-o>gk
map <down> gj
map j gj
imap <down> <C-o>gj
map E ge

" Resize splits with home H/J/K/L
map <S-Left> :vertical res +5<CR>
map <S-Right> :vertical res -5<CR>
map <S-Up> :res +5<CR>
map <S-Down> :res -5<CR>

" Move between splits
map <C-S-Left> <C-w><Left>
map <C-S-Right> <C-w><Right>
map <C-S-Up> <C-w><Up>
map <C-S-Down> <C-w><Down>

" upper/lower word
nmap <leader>L gUiw
nmap <leader>l guiw

" Directory of current file (`:e %%` for example) – Gary Bernhardt
cnoremap %% <C-R>=expand('%:h').'/'<cr>
map <leader>e :edit %%

" Adjust viewports to the same size
map <Leader>= <C-w>=
imap <Leader>= <Esc> <C-w>=i




" **************** Custom functions

function! s:setupFourSpacesIndent()
  set softtabstop=4 shiftwidth=4 tabstop=4
  set noexpandtab|retab
endfunction

function! s:setupTwoSpacesIdent()
  set softtabstop=2 shiftwidth=2 tabstop=2
  set smarttab
  set expandtab|retab
endfunction

" Two spaces indent is default
call s:setupTwoSpacesIdent()

" Remove trailing spaces
function RemoveTrailingSpaces()
  %s/\v[\t ]{-1,}$//g
endfunction
" And create a command for it
command Rts :call RemoveTrailingSpaces()

" Switch spell checking language (via @moonglum)
command! British :set spelllang=en_gb
command! American :set spelllang=en_us
command! German :set spelllang=de

" Preview markdown files with Marked.app
function <SID>previewMarkdown()
  silent execute "!mark % > /dev/null 2>&1 &"
  redraw!
endfunction

" File types that do not contain source code should wrap their text
" and should not have a character limit per line. (via @moonglum)
function! s:setupForProse()
  set wrap linebreak nolist
  match ErrorMsg ''
endfunction 

" When writing markdown create mapping for preview and setup wrapping
function s:setupMarkup()
  call s:setupForProse()
  map <buffer> <Leader>p :call <SID>previewMarkdown()<CR>
endfunction

" Execute the current (Ruby) bugger and display the result in a preview window
function Run()
  let executable=b:current_compiler

  if !getbufvar("%", "&saved")
    let tmpfile = tempname()
    silent! exe "write " . tmpfile . ".rb"
  endif

  let filename=expand('%:p')

  " Create the previewwindow if doesn't exist
  if ! &previewwindow
    pedit /tmp/_vim_previewwindow
  endif
  " Change to the previewwindow
  silent! wincmd P
  " Run the file with and switch back to the previous window
  execute "%d|silent 0r!" . executable . " '" . filename . "'"
  wincmd p
endfunction

map <Leader>r :call Run()<CR>




" **************** Here be autocommands

" Open all folds automatically
autocmd BufRead,BufNewFile * normal zR

augroup vimrcEx
  " Clear all autocmds in the group
  autocmd!
  "Sourced from vim tip: http://vim.wikia.com/wiki/Keep_folds_closed_while_inserting_text
  autocmd InsertEnter * if !exists('w:last_fdm') | let w:last_fdm=&foldmethod | setlocal foldmethod=manual | endif
  autocmd InsertLeave,WinLeave * if exists('w:last_fdm') | let &l:foldmethod=w:last_fdm | unlet w:last_fdm | endif

  " Jump to last cursor position unless it's invalid or in an event handler
  autocmd BufReadPost *
    \ if line("'\"") > 0 && line("'\"") <= line("$") |
    \   exe "normal g`\"" |
    \ endif

  " for ruby, autoindent with two spaces, always expand tabs
  autocmd FileType ruby,haml,eruby,yaml,html,javascript,sass,cucumber set ai sw=2 sts=2 et
  autocmd FileType ruby,javascript set synmaxcol=800
  autocmd FileType python setlocal sw=4 sts=4 noexpandtab

  autocmd! BufRead,BufNewFile *.sass setfiletype sass
  autocmd! BufRead,BufNewFile jquery.*.js set ft=javascript syntax=jquery

  " md, markdown, and mk are markdown and define buffer-local preview
  autocmd BufRead,BufNewFile *.{md,markdown,mdown,mkd,mkdn} call s:setupMarkup()
  autocmd BufRead,BufNewFile *.{md,markdown,mdown,mkd,mkdn} set ft=markdown ai formatoptions=tcroqn2 comments=n:&gt;

  autocmd FileType ruby compiler ruby
  autocmd FileType ruby set foldmethod=syntax
  autocmd FileType css  setlocal foldmethod=indent shiftwidth=2 tabstop=2

  " Makefiles require tabs
  autocmd FileType make setlocal noexpandtab

  " Omni Completion *************************************************************
  autocmd FileType html set omnifunc=htmlcomplete#CompleteTags
  autocmd FileType python set omnifunc=pythoncomplete#Complete
  autocmd FileType javascript set omnifunc=javascriptcomplete#CompleteJS
  autocmd FileType css set omnifunc=csscomplete#CompleteCSS
  autocmd FileType xml set omnifunc=xmlcomplete#CompleteTags
  autocmd FileType php set omnifunc=phpcomplete#CompletePHP
  autocmd FileType c set omnifunc=ccomplete#Complete

  " May require ruby compiled in
  autocmd FileType ruby,eruby set omnifunc=rubycomplete#Complete

  " Set the Ruby filetype for a number of common Ruby files without .rb (via @moonglum)
  au BufRead,BufNewFile {Gemfile,Rakefile,Vagrantfile,Thorfile,Procfile,Guardfile,Capfile,config.ru,*.rake,*.rabl} set ft=ruby

  " Syntax highlighting for JSON
  au BufNewFile,BufRead *.json set ft=javascript

  " Plain text should be wrapped too
  au BufRead,BufNewFile *.txt call s:setupForProse()
augroup END



" At the very end (and after Vundle is finished) re-activate file type detection
filetype plugin indent on
