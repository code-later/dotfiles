unsetopt menu_complete
unsetopt flowcontrol

setopt automenu
setopt complete_in_word
setopt auto_remove_slash
unsetopt always_to_end

WORDCHARS="*?_-.[]~/&;!#$%^(){}<>"  # part of word in line editor

zmodload zsh/complist
autoload -U compinit && compinit
autoload -U colors ; colors

### If you want zsh's completion to pick up new commands in $path automatically
### comment out the next line and un-comment the following 5 lines
zstyle ':completion:::::' completer _complete _approximate
_force_rehash() {
  (( CURRENT == 1 )) && rehash
  return 1 # Because we didn't really complete anything
}
zstyle ':completion:::::' completer _force_rehash _complete _approximate
zstyle ':completion:*' matcher-list 'm:{a-zA-Z}={A-Za-z}' 'r:|[._-]=* r:|=*' 'l:|=* r:|=*'

zstyle ':completion:*:*:*:*:processes' menu yes select
zstyle ':completion:*:*:*:*:processes' force-list always

zstyle -e ':completion:*:approximate:*' max-errors 'reply=( $(( ($#PREFIX + $#SUFFIX) / 3 )) )'
zstyle ':completion:*:descriptions' format "- %d -"
zstyle ':completion:*:corrections' format "- %d - (errors %e})"
zstyle ':completion:*:default' list-prompt '%S%M matches%s'
zstyle ':completion:*' group-name ''
zstyle ':completion:*:manuals' separate-sections true
zstyle ':completion:*:manuals.(^1*)' insert-sections true
zstyle ':completion:*' menu yes select
zstyle ':completion:*' verbose yes

## determine in which order the names (files) should be
## listed and completed when using menu completion.
## `size' to sort them by the size of the file
## `links' to sort them by the number of links to the file
## `modification' or `time' or `date' to sort them by the last modification time
## `access' to sort them by the last access time
## `inode' or `change' to sort them by the last inode change time
## `reverse' to sort in decreasing order
## If the style is set to any other value, or is unset, files will be
## sorted by reverse access time.
## kudos to cypher (https://github.com/cypher/dotfiles/blob/master/zshrc)
zstyle ':completion:*' file-sort reverse access

zstyle :compinstall filename '~/.zsh.d/C60_completion'

compdef '_path_files -/ -W ${~projects_dir}' p

